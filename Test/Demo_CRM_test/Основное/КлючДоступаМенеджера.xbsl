импорт Общие

@Обработчик
метод ПроверитьНаличиеКлючейДоступа(Ключи: ЧитаемыйМассив<КлючДоступаМенеджера.Объект>, ПользователиДляПроверки: ЧитаемыйМассив<Пользователи.Объект>): 
                                        ЧитаемоеСоответствие<КлючДоступаМенеджера.Объект, ЧитаемыйМассив<Пользователи.Ссылка>>
                                        
    знч Регионы = <Регионы.Ссылка>[]
    для Ключ из Ключи
        Регионы.Добавить(Ключ.Владелец)
    ;
    
    знч Пользователи = <Пользователи.Ссылка>[]
    для Пользователь из ПользователиДляПроверки
        Пользователи.Добавить(Пользователь.Ссылка)
    ;
    
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Пользователь,
            Регион
        ИЗ 
            Сотрудники
        ГДЕ 
            Регион В (%Регионы)
            И Пользователь В (%Пользователи)
            И Роль == РольСотрудника.Менеджер}
    
    знч МенеджерыРегионов = <Регионы.Ссылка, Массив<Пользователи.Ссылка>>{:}
    
    знч РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        МенеджерыРегионов.ВставитьЕслиОтсутствует(СтрокаРезультата.Регион, <Пользователи.Ссылка>[])
        МенеджерыРегионов.Получить(СтрокаРезультата.Регион).Добавить(СтрокаРезультата.Пользователь)
    ;
    
    РезультатЗапроса.Закрыть()
    
    знч Результат = <КлючДоступаМенеджера.Объект, Массив<Пользователи.Ссылка>>{:}
    
    для Ключ из Ключи
        знч МассивПользователей = МенеджерыРегионов.ПолучитьИлиУмолчание(Ключ.Владелец)
        если МассивПользователей != Неопределено
            Результат.Вставить(Ключ, МассивПользователей)
        ;
    ;
    
    возврат Результат
;