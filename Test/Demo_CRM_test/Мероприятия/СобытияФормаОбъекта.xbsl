импорт Основное
импорт Пресейл

@Обработчик
метод ПослеЧтения()
    если ЭтоНовый()
        если Сделка != Неопределено
            Объект.Владелец = Сделка
        ;
        знч Сотрудник = ПользователиКлиентИСервер.ПолучитьТекущегоСотрудника()
        если Сотрудник != Неопределено
            Объект.Ответственный = Сотрудник
        ;
    ;
    
    Компоненты.ТаблицаУчастников.Источник.Данные = МассивУчастников()
;

@Обработчик
метод ПередЗаписьюОбъекта()
    пер ПоляНеЗаполнены: Булево
    если Объект.Наименование.Сократить().Пусто()
        Компоненты.ПолеВводаНаименование.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        ПоляНеЗаполнены = Истина
    иначе
        Компоненты.ПолеВводаНаименование.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    ;
    если Объект.Владелец == Неопределено 
        Компоненты.ПолеВводаСделка.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        ПоляНеЗаполнены = Истина
    иначе
        Компоненты.ПолеВводаСделка.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    ;
    если ПоляНеЗаполнены
        выбросить новый ИсключениеВалидации("Не заполнены обязательные поля", ОтображатьСообщениеОбОшибке = Ложь)
    ;

    если ЭтоНовый()
        Объект.ДатаСоздания = Момент.Сейчас()
    иначе
        ПоказыватьУведомление = Ложь
    ;
;

@Обработчик
метод ПослеЗаписиОбъекта()
    МероприятиеЗаписано.Оповестить(КлючОбъекта)
    
    если ПоказыватьУведомление
        знч Уведомление = новый Уведомление(ЛокализованныеСтроки.СозданиеСобытия(), ЛокализованныеСтроки.СозданоСобытие(Объект.Наименование))
        Уведомление.Данные = КлючОбъекта
        Уведомление.ОценкаИнформации = ОценкаИнформации.Положительная
        Уведомление.Показать()
    ;
;

метод МассивУчастников(): Массив<УчастникиМероприятия>
    возврат Объект.Участники
;

метод ПолучитьПредставлениеГлавнойКоманды(): Строка
    возврат ЭтоНовый() ? ЛокализованныеСтроки.Создать() : ЛокализованныеСтроки.Сохранить()
;

метод ДобавитьУчастникаПриНажатии(Источник: Кнопка, Событие: СобытиеПриНажатии)
    знч Клиент = Объект.Владелец != Неопределено ? ПолучитьКлиентаСделки(Объект.Владелец) : Неопределено
    знч ДанныеУчастника = ФормаДобавленияУчастника.ОткрытьВМодальномОкне(Клиент = Клиент)
    если ДанныеУчастника это УчастникиМероприятия
        ДобавитьУчастника(ДанныеУчастника)
    ;
;

метод УдалитьУчастникаПриНажатии(Команда: КомандаСПараметром<Массив<УчастникиМероприятия>>, Параметр: Массив<УчастникиМероприятия>)
    УдалитьУчастника(Параметр.Единственный())
;

метод ДобавитьУчастника(Участник: УчастникиМероприятия)
    если не МассивУчастников().Содержит(Участник)
        МассивУчастников().Добавить(Участник)
    ;
;

метод УдалитьУчастника(Участник: УчастникиМероприятия)
    МассивУчастников().Удалить(Участник)
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьКлиентаСделки(Сделка: Сделки.Ссылка): Клиенты.Ссылка?
    возврат Сделки.ПолучитьКлиентаСделки(Сделка)
;