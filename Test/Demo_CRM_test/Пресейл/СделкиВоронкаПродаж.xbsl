@Обработчик
метод ПослеСоздания()
    ИнициализироватьВоронкуПродаж()
    
    СделкаЗаписана.ПодключитьОбработчик(метод (Сделка) ->
        если Сделка != Неопределено
            ОбработатьЗаписьСделки(Сделка)
        иначе
            // Сделка была удалена
            ИнициализироватьВоронкуПродаж()
        ;
    ;)
;

метод ПриВыбореКоличестваСделок10(Команда: ОбычнаяКоманда)
    ЧислоСделок = 10
    ИнициализироватьВоронкуПродаж()
;

метод ПриВыбореКоличестваСделок20(Команда: ОбычнаяКоманда)
    ЧислоСделок = 20
    ИнициализироватьВоронкуПродаж()
;

метод ПриВыбореКоличестваСделок30(Команда: ОбычнаяКоманда)
    ЧислоСделок = 30
    ИнициализироватьВоронкуПродаж()
;

метод ИнициализироватьВоронкуПродаж()
    знч ВоронкаПродаж = Компоненты.ГруппаВоронкаПродаж

    Столбцы.Очистить()
    КарточкиСделок.Очистить()
    ВоронкаПродаж.Колонки.Очистить()
    ВоронкаПродаж.Содержимое.Очистить()

    знч СтадииСделок = ПолучитьСтадииСделок()
    
    для Индекс = 0 по СтадииСделок.Размер() - 1
        знч Колонка = новый КолонкаМатричнойГруппы()
        ВоронкаПродаж.Колонки.Добавить(Колонка)
    ;

    // Создаем заголовки колонок
    для Стадия из СтадииСделок
        знч ЯчейкаШапки = новый ФиксированнаяГруппа()
        ЯчейкаШапки.Ориентация = ОриентацияСодержимого.Горизонтальная
        ЯчейкаШапки.ВыравниваниеСодержимогоПоВертикали = ВыравниваниеПоВертикали.Центр

        знч ИконкаСтадии = новый Картинка()
        ИконкаСтадии.Изображение = Стадия.Иконка
        
        знч НадписьСтадии = новый Надпись()
        НадписьСтадии.Значение = Стадия.Наименование
        НадписьСтадии.Важность = ВажностьКомпонента.Высокая
        НадписьСтадии.Шрифт = СтилевыеШрифты.ЗаголовокОбычный
        НадписьСтадии.МаксимальнаяВысотаВСтроках = 1
        
        ЯчейкаШапки.Содержимое.Добавить(ИконкаСтадии)
        ЯчейкаШапки.Содержимое.Добавить(НадписьСтадии)
        
        ВоронкаПродаж.Содержимое.Добавить(ЯчейкаШапки)
    ;
    
    // Создаем группы - контейнеры для карточек
    для Стадия из СтадииСделок
        знч ГруппаКонтейнер = новый ФиксированнаяГруппа()
        ГруппаКонтейнер.Содержимое.Добавить(новый СтатистикаСтадииСделокКомпонент())
        
        ВоронкаПродаж.Содержимое.Добавить(ГруппаКонтейнер)
        Столбцы.Вставить(Стадия.Ссылка, ГруппаКонтейнер)
    ;

    // Заполняем данными сделок
    знч ДанныеСделок = ПолучитьДанныеСделок(КоличествоЗаписей = ЧислоСделок)
    для ДанныеСделки из ДанныеСделок
        ДобавитьСделку(ДанныеСделки)
    ;
    
    // Обновляем статистику столбцов
    для Столбец из Столбцы
        ОбновитьСтатистикуСтолбца(Столбец.Значение)
    ;
    
    // Заполняем массив элементов меню выбора стадии для использования в каждой карточке
    ИнициализироватьМенюИзмененияСтадии()
;

метод ИнициализироватьМенюИзмененияСтадии()
    знч СтадииСделок = ПолучитьСтадииСделок(Истина)
    
    ЭлементыМенюИзмененияСтадии.Очистить()
    для Стадия из СтадииСделок
        знч ЭлементМеню = новый ЭлементМеню()
        ЭлементМеню.Заголовок = Стадия.Наименование
        ЭлементМеню.ДополнительныеПараметры = Стадия.Ссылка
        если Стадия.Иконка != Неопределено
            ЭлементМеню.Изображение = Стадия.Иконка
        ;
        ЭлементыМенюИзмененияСтадии.Добавить(ЭлементМеню)
    ;
;

метод ОбработатьЗаписьСделки(Сделка: Сделки.Ссылка)
    знч ДанныеСделки = ПолучитьДанныеСделки(Сделка)
    знч Карточка = КарточкиСделок.ПолучитьИлиУмолчание(Сделка)
    знч НовыйСтолбец = Столбцы.ПолучитьИлиУмолчание(ДанныеСделки.Стадия)

    // Новая сделка
    если Карточка == Неопределено 
        если НовыйСтолбец != Неопределено 
            знч НоваяКарточка = ДобавитьСделку(ДанныеСделки)
            ВыделитьКарточку(НоваяКарточка)
            ОбновитьСтатистикуСтолбца(НовыйСтолбец)
        ;
    // Столбец новой стадии не отображается
    иначе если НовыйСтолбец == Неопределено
        знч СтарыйСтолбец = Столбцы.ПолучитьИлиУмолчание(Карточка.ДанныеСделки.Стадия)
        СтарыйСтолбец.Содержимое.Удалить(Карточка)
        ОбновитьСтатистикуСтолбца(СтарыйСтолбец)
    // Сделка записана без изменения стадии
    иначе если Карточка.ДанныеСделки.Стадия == ДанныеСделки.Стадия
        Карточка.ДанныеСделки = ДанныеСделки
        ВыделитьКарточку(Карточка)
        ОбновитьСтатистикуСтолбца(НовыйСтолбец)
    // Сделка записана с изменением стадии
    иначе
        знч СтарыйСтолбец = Столбцы.ПолучитьИлиУмолчание(Карточка.ДанныеСделки.Стадия)
        СтарыйСтолбец.Содержимое.Удалить(Карточка)
        НовыйСтолбец.Содержимое.Добавить(Карточка)
        Карточка.ДанныеСделки = ДанныеСделки
        ВыделитьКарточку(Карточка)
        ОбновитьСтатистикуСтолбца(СтарыйСтолбец)
        ОбновитьСтатистикуСтолбца(НовыйСтолбец)
    ;
;

метод ДобавитьСделку(ДанныеСделки: ДанныеСделки): Карточка
    знч МенюИзмененияСтадии = новый ВсплывающееМеню()
    МенюИзмененияСтадии.Элементы = ЭлементыМенюИзмененияСтадии
    
    знч Карточка = новый СделкиКарточка(ДанныеСделки = ДанныеСделки, МенюИзмененияСтадии = МенюИзмененияСтадии)
    Карточка.ПриИзмененииСтадии = &ПриИзмененииСтадииСделкиВКарточке
    
    знч Столбец = Столбцы.Получить(ДанныеСделки.Стадия)
    Столбец.Содержимое.Добавить(Карточка)
    
    КарточкиСделок.Вставить(ДанныеСделки.Ссылка, Карточка)
    
    возврат Карточка
;

метод ПриИзмененииСтадииСделкиВКарточке(Карточка: СделкиКарточка, Событие: СобытиеСДанными<СтадииСделок.Ссылка>)
    знч НоваяСтадия = Событие.Данные
    знч СтараяСтадия = Карточка.ДанныеСделки.Стадия
    
    если НоваяСтадия != СтараяСтадия
        ИзменитьСтадиюСделки(Карточка.ДанныеСделки.Ссылка, НоваяСтадия)
        
        знч СтарыйСтолбец = Столбцы.Получить(СтараяСтадия)
        СтарыйСтолбец.Содержимое.Удалить(Карточка)
        ОбновитьСтатистикуСтолбца(СтарыйСтолбец)
        
        знч НовыйСтолбец = Столбцы.ПолучитьИлиУмолчание(НоваяСтадия)
        если НовыйСтолбец != Неопределено
            Карточка.ДанныеСделки.Стадия = НоваяСтадия
            НовыйСтолбец.Содержимое.Добавить(Карточка)
            ОбновитьСтатистикуСтолбца(НовыйСтолбец)
            ВыделитьКарточку(Карточка)
        ;
    ;
;

метод СоздатьСделкуПриНажатии(Команда: ОбычнаяКоманда)
    ФормаСозданияСделки.ОткрытьВМодальномОкне()
;

метод СоздатьКлиентаПриНажатии(Команда: ОбычнаяКоманда)
    КлиентыФормаОбъекта.ОткрытьВМодальномОкне()
;

метод ОбновитьПриНажатии(Команда: ОбычнаяКоманда)
    ИнициализироватьВоронкуПродаж()
;

метод ОбновитьСтатистикуСтолбца(Столбец: Группа)
    пер КоличествоСделок: Число
    пер СуммаСделок: Число
    для Индекс = 1 по Столбец.Содержимое.Размер() - 1
        КоличествоСделок += 1
        знч КарточкаСделки = Столбец.Содержимое[Индекс] как СделкиКарточка
        СуммаСделок += КарточкаСделки.ДанныеСделки.СуммаВВалютеУчета 
    ;
    знч Статистика = новый СтатистикаСтадииСделокКомпонент(КоличествоСделок = КоличествоСделок, СуммаСделок = СуммаСделок)
    Столбец.Содержимое.Установить(0, Статистика)
;

метод ВыделитьКарточку(Карточка: Карточка)
    для Элемент из КарточкиСделок
        Элемент.Значение.ОценкаИнформации = ОценкаИнформации.Обычная
    ;
    Карточка.ОценкаИнформации = ОценкаИнформации.Положительная
;

@НаСервере @НаКлиенте
структура СтадияСделки
    обз знч Ссылка: СтадииСделок.Ссылка 
    обз знч Наименование: Строка
    обз знч Иконка: ДвоичныйОбъект.Ссылка?
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьСтадииСделок(ВключитьПроигранные: Булево = Ложь): ЧитаемыйМассив<СтадияСделки>
    знч МассивСтадий = <СтадияСделки>[]
    
    знч Запрос = Запрос{
        ВЫБРАТЬ 
            Ссылка,
            Наименование,
            Иконка
        ИЗ 
            СтадииСделок
        ГДЕ
            %ВключитьПроигранные ИЛИ Вид != ВидСтадииСделки.ЗакрытаПроиграна
        УПОРЯДОЧИТЬ ПО 
            Порядок}
    
    для СтрокаРезультата из Запрос.Выполнить()
        МассивСтадий.Добавить(новый СтадияСделки(СтрокаРезультата.Ссылка, СтрокаРезультата.Наименование, СтрокаРезультата.Иконка)) 
    ;
    
    возврат МассивСтадий
;

@НаСервере @ДоступноСКлиента
статический метод ИзменитьСтадиюСделки(Сделка: Сделки.Ссылка, Стадия: СтадииСделок.Ссылка)
    исп Транзакции.Начать()
    знч СделкаОбъект = Сделка.ЗагрузитьОбъект(Истина)
    СделкаОбъект.Стадия = Стадия
    СделкаОбъект.Записать(Ложь)
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьДанныеСделки(Сделка: Сделки.Ссылка): ДанныеСделки
    возврат Сделки.ПолучитьДанныеСделок([Сделка]).Единственный()
;

@НаСервере @ДоступноСКлиента
статический метод ПолучитьДанныеСделок(КоличествоЗаписей: Число): ЧитаемыйМассив<ДанныеСделки>
    знч Запрос = Запрос{
        ВЫБРАТЬ ПЕРВЫЕ %КоличествоЗаписей
            Ссылка
        ИЗ
            Сделки
        ГДЕ
            Стадия.Вид != ВидСтадииСделки.ЗакрытаПроиграна
        УПОРЯДОЧИТЬ ПО
            ДатаСоздания УБЫВ
    }
    
    знч МассивСделок = <Сделки.Ссылка>[]
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        МассивСделок.Добавить(СтрокаРезультата.Ссылка)
    ;
    знч ДанныеСделок = Сделки.ПолучитьДанныеСделок(МассивСделок)
    возврат ДанныеСделок
;
