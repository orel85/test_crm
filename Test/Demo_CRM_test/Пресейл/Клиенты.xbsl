импорт Основное
импорт Общие

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат [ ПользователиКлиентИСервер.ПолныеПраваНаСправочникДляАутентифицированных() ]
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Объекты: ЧитаемыйМассив<Клиенты.ДанныеРасчетаРазрешений>): Соответствие<Клиенты.ДанныеРасчетаРазрешений, Массив<РазрешениеДоступа>>
    знч Результат = <Клиенты.ДанныеРасчетаРазрешений, Массив<РазрешениеДоступа>>{:}
    
    для Объект из Объекты
        знч Разрешения = <РазрешениеДоступа>[
            ПользователиКлиентИСервер.ПолныеПраваНаСправочникДляРуководителя(),
            новый РазрешениеДоступа(
                [новый КлючДоступаМенеджера.Объект(Владелец = Объект.Регион)],
                [Сущность.Право.Создание, Сущность.Право.Чтение, Сущность.Право.Изменение, Сущность.Право.Удаление])
        ]

        Результат.Вставить(Объект, Разрешения)
    ;

    возврат Результат
;

@ВПодсистеме
метод ПолучитьКонтактныеЛицаКлиента(Клиент: Клиенты.Ссылка): ЧитаемаяКоллекция<КонтактныеЛица.Ссылка>
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Ссылка
        ИЗ
            КонтактныеЛица
        ГДЕ
            Владелец == %Клиент
    }
    
    знч КонтактныеЛица = <КонтактныеЛица.Ссылка>[]
    
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        КонтактныеЛица.Добавить(СтрокаРезультата.Ссылка)
    ;
    
    возврат КонтактныеЛица
;

@ВПодсистеме
метод ПолучитьСделкиКлиента(Клиент: Клиенты.Ссылка): ЧитаемаяКоллекция<Сделки.Ссылка>
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Ссылка
        ИЗ
            Сделки
        ГДЕ
            Клиент == %Клиент
    }
    
    знч Сделки = <Сделки.Ссылка>[]
    
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        Сделки.Добавить(СтрокаРезультата.Ссылка)
    ;
    
    возврат Сделки
;

@ВПроекте
метод ПолучитьРегионыКлиентов(Клиенты: Коллекция<Клиенты.Ссылка>): ЧитаемоеСоответствие<Клиенты.Ссылка, Регионы.Ссылка>
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Ссылка КАК Клиент,
            Регион КАК Регион
        ИЗ
            Клиенты
        ГДЕ
            Ссылка В (%Клиенты)
    }
    
    знч РегионыКлиентов = <Клиенты.Ссылка, Регионы.Ссылка>{:}
    
    исп РезультатЗапроса = Запрос.Выполнить()
    для СтрокаРезультата из РезультатЗапроса
        РегионыКлиентов.Вставить(СтрокаРезультата.Клиент, СтрокаРезультата.Регион)
    ;
    
    возврат РегионыКлиентов
;