импорт Основное

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат [ПользователиКлиентИСервер.ПолныеПраваНаСправочникДляАутентифицированных()]
;

@Обработчик
метод ВычислитьРазрешенияДоступаДляОбъектов(Объекты: ЧитаемыйМассив<КонтактныеЛица.ДанныеРасчетаРазрешений>): Соответствие<КонтактныеЛица.ДанныеРасчетаРазрешений, Массив<РазрешениеДоступа>>
    знч Результат = <КонтактныеЛица.ДанныеРасчетаРазрешений, Массив<РазрешениеДоступа>>{:}
    
    знч КлиентыКонтактныхЛиц = <Клиенты.Ссылка>{}
    
    для Объект из Объекты
        КлиентыКонтактныхЛиц.Добавить(Объект.Владелец)
    ;
    
    знч РегионыКлиентов = Клиенты.ПолучитьРегионыКлиентов(КлиентыКонтактныхЛиц)
    
    для Объект из Объекты
        знч Разрешения = <РазрешениеДоступа>[
            ПользователиКлиентИСервер.ПолныеПраваНаСправочникДляРуководителя(),
            новый РазрешениеДоступа(
                [новый КлючДоступаМенеджера.Объект(Владелец = РегионыКлиентов.Получить(Объект.Владелец))],
                [Сущность.Право.Создание, Сущность.Право.Чтение, Сущность.Право.Изменение, Сущность.Право.Удаление])
        ]

        Результат.Вставить(Объект, Разрешения)
    ;

    возврат Результат
;